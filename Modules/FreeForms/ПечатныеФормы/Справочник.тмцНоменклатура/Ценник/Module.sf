//Модуль загружается до загрузки любой формы и остается в памяти до закрытия всех связанных с ним (либо с его потомками) форм.
//Переменные, объявленные с постфиксом export, будут доступны в любой форме или в модулях и формах потомках.
//Модуль может содержать общие функции, используемые формами, или специальные предопределенные функции, вызываемые системой при наступлении разнообразных событий.
//Предопределенные функции имеют свой набор переменных. Дальнейшее описание ищите в документации.

//Размерность ценника 1.625 / 1
//С учетом границ на листе А4 типа ландшафт это 4 штуки на 4 штуки (примерно)
//Ценники со скидкой 4 шт на 3 шт
Var Т Export;
Var Ориентация Export;
Var Таб Export;
Var флБезШК Export;
Var флБезМагазина Export;
Var Магазин Export;
Var пШК Export;
Var ЮрЛицо Export;
Var ВКолонтСтр Export;

Function OnFinishCopyObject(Name,Index)
  If (Name="пЭтикетка")And(Trim(пШК)<>"") Then
    Try
      iX:=0;iY:=0;
      Generate1DBarcode("EAN",iX,iY,Trim(пШК),"BR:1,F:Consolas|8,SCALE:5,BRT:0");
      iY:=iX*(Т.Objects(Index).Height/Т.Objects(Index).Width);
      Картинка:=Generate1DBarcode("EAN",iX,iY,Trim(пШК),"BR:1,F:Consolas|8,SCALE:5,BRT:0,BPP:32");
      Т.Objects(Index).Picture.Assign(Картинка);
    Except
    EndTry;
  EndIf;
EndFunction


Function Печать(флСкидка=0)
  Т:=Table.Create();
  If флБезМагазина Then
    Т.SetSourceName("БезОтвЛица");
  Else
    Т.SetSourceName(?(флСкидка=0,"Table","Скидка"));
  EndIf;  
  нКол:=1;
  нСтр:=1;
  
  пДатаЦены:=FormatDate(CurDate(),"dd.mm.yy");
  If isEmpty(ЮрЛицо) Then
    пЮрЛицо:=Trim(Магазин.ЮрЛицо.Контрагент.ПолнНаименование);
  ElseIf isDbObject(ЮрЛицо) Then
    пЮрЛицо:=Trim(ЮрЛицо.Контрагент.ПолнНаименование);
  Else
    пЮрЛицо:=ЮрЛицо;
  EndIf;  
  пОтвЛицо:="Ответственный: "+Trim(глПользователь.Name);
  
  кЦ:=Ref.цПечатьЦенников;
  ФлВывод:=0;
  Таб.Select();
  While Таб.Next() Do
    //Скидка?
    Скидка:=0;
    If Таб.GetColumnIndex("Скидка")>0 Then
      Скидка:=Number(Таб.Скидка);
    EndIf;
    If флСкидка<>(Скидка>0) Then
      Continue;
    EndIf;
    пТМЦ:=Trim(Таб.Номенклатура.Name);
    пШК:=Таб.ШтрихКод;
    пЦена:=""+Trunc(Таб.Цена);
    пКоп:=Right("00"+Round((Таб.Цена-Trunc(Таб.Цена))*100),2);
    
    If Скидка>0 Then
      пСтЦена:=Таб.Цена+Скидка;
      пКоп2:=Right("00"+Round((пСтЦена-Trunc(пСтЦена))*100),2);
      пСтЦена:="Старая цена "+пСтЦена+"-"+пКоп2;
    EndIf;
    
    пЕдИзм:="ед.: "+?(Таб.Номенклатура.базЕдиница.Selected()<>0,Trim(Таб.Номенклатура.базЕдиница),"шт");
    пСтрана:="Страна: "+?(Таб.Номенклатура.сертСтранаПроисхождения.Selected()=0,"Россия",Trim(Таб.Номенклатура.сертСтранаПроисхождения));
    пКод:=глНомерБезНулей(Таб.Номенклатура);
    ТипТаб:=?((пШК=0)Or(пШК="")Or(флБезШК=1),"v2","v1");
    
    пPLU:="";
    if not флБезМагазина Then 
      аПл:=Ref.тмцPLUКоды;
      аМагазин:=Магазин;
      If аПл.Find("~(@Parent=Таб.Номенклатура)And(Магазин=аМагазин)And(@Status=0)")>0 Then
        пPLU:=""+аПл.PLUКод;
      EndIf;
    EndIf;  
    
    ФлВывод:=1;
    If нКол=1 Then
      Т.CopyByX("h1|"+ТипТаб,1);
      нКол:=2;
    ElseIf нКол<5 Then
      нКол:=нКол+1;
      Т.CopyByX("h1|"+ТипТаб);
    ElseIf нСтр<?(флСкидка=0,4,3) Then
      нКол:=2;
      нСтр:=нСтр+1;
      Т.CopyByX("h1|"+ТипТаб,1);
    Else
      нКол:=2;
      нСтр:=1;
      Т.PageBreaks.Add();
      Т.CopyByX("h1|"+ТипТаб,1);
    EndIf;
  EndDo;
  
  If ФлВывод=0 Then
    Exit;
  EndIf;
  
  While нКол<5 Do
    нКол:=нКол+1;
    Т.CopyByX("h1|v3");//Ширина страницы
  EndDo;
  If ВКолонтСтр<>"" Then
    ВКолонтСтр:=ВКолонтСтр+", ";
  EndIf;
  Т.CopyTitles();
  
  Т.Printing.Orientation:=1;
  Т.Printing.ScaleMode:=3;
  глПечатнаяФорма(Т,Param,"Печать ценников"+?(флСкидка=0,""," (скидка)"),?(IsEmpty(Ориентация)=0,Ориентация,-1));
EndFunction

Function OnOpen()
  Result:=0;
  ВКолонтСтр:="";
  If not IsEmpty(Param) Then
    Объект:=Param["Object"];
    If Pos("DB.REF",TypeStr(Объект))=1 Then
      If _And(глПользователь.Магазин.Selected()=0,not глПользователь.общиеАдминистратор) Then
        Box("Для вашего пользователя не выбран магазин!",Q_STOP);
        Exit;
      EndIf;
    
      Магазин=глПользователь.Магазин;
      If Магазин.Selected()=0 Then
        Магазин:=SelectObject("Ref.рознМагазины",0,0,"Module","SelectForm",,,"Выберите магазин:");
        If isEmpty(Магазин) Then
          Exit;
        EndIf;
      EndIf;
      ТипЦен:=Магазин.ТипЦенРеализации;
      If ТипЦен.Selected()=0 Then
        Box("Для магазина не выбран тип цен реализации!",Q_STOP);
        Exit;
      EndIf;
      
      Таб:=Tab.Create("Номенклатура,ШтрихКод,Цена");
      флБезШК:=0;
      Таб.AddLine("Номенклатура,ШтрихКод,Цена",Объект,глПолучитьШтрихкод(Объект),глНайтиЦену(Объект,ТипЦен,CurDate()));
    ElseIf TypeStr(Объект)<>"TAB" Then
      Exit;
    Else
      Таб:=Объект;
      флБезШК:=Number(Param["БезШК"]);
      флБезМагазина:=Number(Param["флБезМагазина"]);
      ТипЦен:=Param["ТипЦен"];
      Магазин:=Param["Магазин"];
      Ориентация:=Param["Ориентация"];
      ЮрЛицо:=Param["ЮрЛицо"];
      ВКолонтСтр:=Str(Param["Колонтитул"]);
    EndIf;
    
    Печать(0);
    If not флБезМагазина Then
      Печать(1);
    EndIf;
    Param["Ответ"]:=1;
  EndIf;
EndFunction


