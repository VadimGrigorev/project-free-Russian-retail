//Модуль загружается до загрузки любой формы и остается в памяти до закрытия всех связанных с ним (либо с его потомками) форм.
//Переменные, объявленные с постфиксом export, будут доступны в любой форме или в модулях и формах потомках.
//Модуль может содержать общие функции, используемые формами, или специальные предопределенные функции, вызываемые системой при наступлении разнообразных событий.
//Предопределенные функции имеют свой набор переменных. Дальнейшее описание ищите в документации.

Function ПриПодбореЭлементов(aEl,Фл)
  оЭлементы:=Form.оЭлементы.Value;
  
  If isEmpty(aEl) Then
    Exit;
  EndIf;
  а:=оЭлементы.Find(aEl);
  If а>0 Then
    Box("Элемент "+Str(aEl)+" уже был выбран!",Q_STOP);
  Else
    оЭлементы.Add(aEl);
  EndIf;  
EndFunction


Function OnExecute()
  оБаза:=Form.оБаза.Value;
  оЭлементы:=Form.оЭлементы.Value;
  
  If оБаза.Selected()=0 Then
    Box("Выберите периферийную базу, для которой будет создан запрос!",Q_STOP);
    Exit;
  EndIf;
  КлИБ:=Trim(оБаза.ПрефиксБазы);
  If КлИБ="" Then
    Box("Выбранная периферийная база не имеет префикса!",Q_STOP);
    Exit;
  EndIf;
  
  iNum:=0;
  For i:=оЭлементы.Size() DownTo 1 Do
    If оЭлементы.Check(i)=0 Then
      Continue;
    EndIf;
    Эл:=оЭлементы.Get(i);
    If isEmpty(Эл) Then
      Continue;
    EndIf;
    в:=Ref.рбСопоставленныеЭлементы;
    If not в.Find("Элемент,ПрефиксУдаленнойБазы,флЗапретОбновленияРеквизитов",Эл,КлИБ,0) Then
      Continue;
    EndIf;
    //Уже есть?
    //ПрефиксУдБазы # ВидСправочника # КодУдБазы
    в2:=Ref.рбНесинхронизированныеЭлементы;
    Инд:=Trim(КлИБ)+"#"+Эл.DBName()+"#"+Trim(в.КодУдаленнойБазы);
    If в2.Find("Индекс",Инд) Then
      Continue;
    EndIf;
    элКонт:=глНайтиКонтейнерСопоставления(Trim(КлИБ),Эл.DBName());
    If элКонт=0 Then
      Exit;
    EndIf;
    в2.New();
    в2.Folder(элКонт);
    в2.ПрефиксУдаленнойБазы:=Trim(КлИБ);
    в2.КодУдаленнойБазы:=Trim(в.КодУдаленнойБазы);
    в2.ВидСправочника:=Эл.DBName();
    в2.Флаг:=6;
    в2.Индекс:=Trim(в.Индекс);
    If not глЗаписать(в2) Then
      Continue;
    EndIf;
    Form.StatusText(Эл);
    iNum:=iNum+1;
  EndDo;
  Message("Обработка успешно завершена, запросов: "+iNum,"I");
  оЭлементы.Clear();
  Box("Было создано "+iNum+" запросов на обновление!",Q_INFORMATION);
EndFunction
