//Модуль загружается до загрузки любой формы и остается в памяти до закрытия всех связанных с ним (либо с его потомками) форм.
//Переменные, объявленные с постфиксом export, будут доступны в любой форме или в модулях и формах потомках.
//Модуль может содержать общие функции, используемые формами, или специальные предопределенные функции, вызываемые системой при наступлении разнообразных событий.
//Предопределенные функции имеют свой набор переменных. Дальнейшее описание ищите в документации.
Var оТУлиц Export;
Var оТГородов Export;
Var ЧлИзмененных Export, ЧлДобавленных Export;
Var флДлинаКода Export;

//Папка: 0 -- элемент
//       1 -- папка (район)
//       2 -- папка города, внутри которой необходимо создать сам город
Function ЗагрузитьГород(ByRef ФлНовый,База,Владелец,Папка=0,Родитель=0)
  флИмена:=Form.оИмена.Value;
  Имя:=Left(Trim(База.NAME),50);
  Код:=Left(База.CODE,флДлинаКода);
  ТНП:=оТГородов.GetByName(Trim(LowerCase(База.SOCR)));
  If IsEmpty(ТНП) Then
    ТНП:=оТГородов.GetByName("0");
  EndIf;
  
  аЭл:=Ref.адГорода;
  Фл:=1;
  If _Or(ФлНовый,not аЭл.Find("~(@Parent=Владелец)And(КодБазы=Код)And(@IsFolder="+(Папка>0)+")")) Then
    If _Or(ФлНовый,not аЭл.Find("~(@Parent=Владелец)"+?(Родитель<>0,"And(@Folder=Родитель)")+"And(Name=Имя)And(ТипНаселенногоПункта=ТНП)And(@IsFolder="+(Папка>0)+")")) Then
      аЭл.New(Папка>0);
      аЭл.Parent(Владелец);
      If Родитель<>0 Then
        аЭл.Folder(Родитель);
      EndIf;
      ЧлДобавленных:=ЧлДобавленных+1;
      ФлНовый:=1;
      Фл:=0;
    EndIf;
  EndIf;
  If Фл Then
    If _And(аЭл.ТипНаселенногоПункта=ТНП,флИмена Or(аЭл.Name=Имя),аЭл.Индекс=База.INDEX,аЭл.КодБазы=Код) Then
      Exit аЭл;
    EndIf;
    ЧлИзмененных:=ЧлИзмененных+1;
    ФлНовый:=0;
  EndIf;
  аЭл.ТипНаселенногоПункта:=ТНП;
  If (not флИмена)Or(аЭл.Name="") Then
    аЭл.Name:=Имя;
  EndIf;
  аЭл.Индекс:=База.INDEX;
  аЭл.КодБазы:=Код;
  аЭл.Save();
  If Папка=2 Then
    ЗагрузитьГород((ФлНовый),База,Владелец,0,аЭл);
  EndIf;
  
  Exit аЭл;
EndFunction
      


Function OnExecute()
  оГорода:=Form.оГорода.Value;
  If Trim(оГорода)="" Then
    Box("Не указан путь к файлу KLADR.DBF",Q_STOP);
    Exit;
  EndIf;
  СтрРегион:=Trim(Form.оРегион.Value);
  If СтрРегион="" Then
    If AskQuestion("Рекомендуется выбрать регион для загрузки. Если не выбрать регион, загрузка может занять долгое время (ее можно прервать с помощью Ctrl+C). "+
        "Продолжить без выбора региона (не рекомендуется)?",Q_WARNING+Q_YESNO)<>R_YES Then
      Exit;
    EndIf;
  EndIf;
  TearStr(СтрРегион,":");
  СтрРегион:=Trim(СтрРегион);
  оПапка:=Form.оПапка.Value;
  If оПапка.Selected()<>0 Then
    If Trim(оПапка.КодБазы)="" Then
      Box("Элементы введенные вручную нельзя использовать как ограничители при загрузке индентификатора!",Q_STOP);
      Form.оПапка.Value:="";
      Exit;
    EndIf;
    СтрРегион:=Left(Trim(оПапка.КодБазы),2);
    пРегион:="";  
  EndIf;
  If СтрРегион<>"" Then
    Message("Ограничение загрузки: "+СтрРегион);
  EndIf;
  
  База:=Dbf.Create();
  База.Open(оГорода);
  База.CodePage:=?(Form.о866.Value=1,866,1251);

  Message("Загружается справочник адАдминистративныеДеления...");
  ТДеление:=Tab.Create("Эл,КодБазы");
  
  флДлинаКода:=Struct.Ref("адАдминистративныеДеления").Props("КодБазы").DataType;
  tearStr(флДлинаКода,"STRING.");
  флДлинаКода:=Number(флДлинаКода);
  ЧлИзмененных:=0;
  ЧлДобавленных:=0;
  Чл0:=0;
  База.First();
  База.Select();
  While База.Next() Do
    Чл0:=Чл0+1;
    If Чл0%100=0 Then
      Form.StatusText(Чл0);
    EndIf;
    Код:=Left(База.CODE,флДлинаКода);
    If Number(Mid(База.CODE,3))<>0 Then
      Continue;
    EndIf;
    ТД:=Left(Trim(База.SOCR),5);
    Имя:=Left(Trim(База.NAME),50);
    КодЭл:=Number(Left(Код,2));
    
    Фл:=1;
    аЭл:=Ref.адАдминистративныеДеления;
    If not аЭл.Find("Code",КодЭл) Then
      If not аЭл.Find("Name,ТипДеления",Имя,ТД) Then
        аЭл.New();
        ЧлДобавленных:=ЧлДобавленных+1;
        Фл:=0;
      EndIf;
    EndIf;
    If Фл Then
      If _And(аЭл.Name=Имя,аЭл.Индекс=База.INDEX,аЭл.КодБазы=Код) Then
        ТДеление.AddLine("Эл,КодБазы",аЭл,аЭл.КодБазы);
        Continue;
      EndIf;
      ЧлИзмененных:=ЧлИзмененных+1;
    EndIf;
    аЭл.Code:=КодЭл;
    аЭл.ТипДеления:=ТД;
    аЭл.Name:=Имя;
    аЭл.Индекс:=База.INDEX;
    аЭл.КодБазы:=Код;
    аЭл.Save();
    
    ТДеление.AddLine("Эл,КодБазы",аЭл,аЭл.КодБазы);
  EndDo;
  ВсегоЗаписей:=Чл0;
  ТДеление.Sort("КодБазы");
  Message("Добавлено элементов: "+ЧлДобавленных);
  Message("Изменено элементов: "+ЧлИзмененных);
  
  
  
  If Form.оТолькоУлицы.Value=0 Then 
    Message("Загружается справочник адГорода...");
    флДлинаКода:=Struct.Ref("адГорода").Props("КодБазы").DataType;
    tearStr(флДлинаКода,"STRING.");
    флДлинаКода:=Number(флДлинаКода);
    
    ФлОк:=0;
    BeginTransaction();
    Try
      //Папки первого уровня
      ТРод:=Tab.Create("Эл,КодБазы,ФлНовый");
      Message("  Загружаются административные районы...");
      ЧлИзмененных:=0;
      ЧлДобавленных:=0;
      Чл0:=0;
      База.First();
      База.Select();
      While База.Next() Do
        Чл0:=Чл0+1;
        If Чл0%100=0 Then
          Form.StatusText(""+Чл0+" из "+ВсегоЗаписей);
        EndIf;
        If (ЧлДобавленных+ЧлИзмененных)%500=0 Then
          CommitTransaction();
          BeginTransaction();
        EndIf;
        Код:=LowerCase(Trim(База.CODE));
        If СтрРегион<>"" Then
          If Left(Код,Length(СтрРегион))<>СтрРегион Then
            Continue;
          EndIf;
        EndIf;
        If Number(Mid(Код,3))=0 Then
          Continue;
        EndIf;
        If Number(Mid(Код,6))<>0 Then
          Continue;
        EndIf;

        If ТДеление.FindAndGoto(Left(Код,2)+"000000000",,"КодБазы",1) Then
          ФлНовый:=0;
          аЭл:=ЗагрузитьГород(ФлНовый,База,ТДеление.Эл,1);
          ТРод.AddLine("Эл,КодБазы,ФлНовый",аЭл,аЭл.КодБазы,ФлНовый);
        EndIf;
      EndDo;
      ТРод.Sort("КодБазы");
      
      
      ТРод2:=Tab.Create("Эл,КодБазы,ФлНовый");
      Message("  Загружаются подразделения второго уровня...");
      Чл0:=0;
      База.First();
      База.Select();
      While База.Next() Do
        Чл0:=Чл0+1;
        If Чл0%100=0 Then
          Form.StatusText(""+Чл0+" из "+ВсегоЗаписей);
        EndIf;
        If (ЧлДобавленных+ЧлИзмененных)%500=0 Then
          CommitTransaction();
          BeginTransaction();
        EndIf;
        Код:=LowerCase(Trim(База.CODE));
        If СтрРегион<>"" Then
          If Left(Код,Length(СтрРегион))<>СтрРегион Then
            Continue;
          EndIf;
        EndIf;
        If Number(Mid(Код,6))=0 Then
          Continue;
        EndIf;
        If Number(Mid(Код,9))<>0 Then
          Continue;
        EndIf;
        If ТДеление.FindAndGoto(Left(Код,2)+"000000000",,"КодБазы",1) Then
          аРайон:=Ref.адГорода;
          If ТРод.FindAndGoto(Left(Код,5)+"000000",,"КодБазы",1)>0 Then
            ФлНовый:=ТРод.ФлНовый;
            //В новых базах KLADR есть ошибки
            ФлТипПапки:=2;
            If Trim(LowerCase(База.SOCR))="с/п" Then
              ФлТипПапки:=0;
            EndIf;
            аЭл:=ЗагрузитьГород(ФлНовый,База,ТДеление.Эл,ФлТипПапки,ТРод.Эл);
            If ФлТипПапки=0 Then
              аЭл:=ТРод.Эл;
            EndIf;
          ElseIf аРайон.Find("@Parent,@IsFolder,КодБазы",ТДеление.Эл,1,Left(Код,11)) Then
            ФлНовый:=0;
            ЗагрузитьГород(ФлНовый,База,ТДеление.Эл,0,аРайон);
            аЭл:=аРайон;
          Else
            ФлНовый:=0;
            аЭл:=ЗагрузитьГород(ФлНовый,База,ТДеление.Эл,2);
          EndIf;
          ТРод2.AddLine("Эл,КодБазы,ФлНовый",аЭл,аЭл.КодБазы,ФлНовый);
        EndIf;
      EndDo;
      ТРод2.Sort("КодБазы");      
    
      If Form.оСпец.Value=1 Then
        База.Close();
        Message("Добавлено элементов: "+ЧлДобавленных);
        Message("Изменено элементов: "+ЧлИзмененных);
        Message("Работа окончена!");
        ФлОк:=1;
        Exit;
      EndIf;
    
      Message("  Загружаются города...");
      ПоследнийЗагруженныйКод:="";
      Чл0:=0;
      База.First();
      База.Select();
      While База.Next() Do
        Чл0:=Чл0+1;
        If Чл0%100=0 Then
          Form.StatusText(""+Чл0+" из "+ВсегоЗаписей);
        EndIf;
        If (ЧлДобавленных+ЧлИзмененных)%500=0 Then
          CommitTransaction();
          BeginTransaction();
        EndIf;
        ТекущийКод:=Left(База.CODE,Length(База.CODE)-2);
        If ПоследнийЗагруженныйКод=ТекущийКод Then//Две последние цифры -- мусор
          Continue;
        EndIf;
        ПоследнийЗагруженныйКод:=ТекущийКод;
        
        Код:=LowerCase(Trim(База.CODE));
        If СтрРегион<>"" Then
          If Left(Код,Length(СтрРегион))<>СтрРегион Then
            Continue;
          EndIf;
        EndIf;
        If Number(Mid(Код,9))=0 Then
          Continue;
        EndIf;
        If ТДеление.FindAndGoto(Left(Код,2)+"000000000",,"КодБазы",1) Then
          Фл:=0;
          ФлНовый:=0;
          If ТРод2.FindAndGoto(Left(Код,8)+"000",,"КодБазы",1) Then
            Фл:=1;
            Род:=ТРод2.Эл;
            ФлНовый:=ТРод2.ФлНовый;
          ElseIf ТРод.FindAndGoto(Left(Код,5)+"000000",,"КодБазы",1)>0 Then
            Фл:=1;
            Род:=ТРод.Эл;
            ФлНовый:=ТРод.ФлНовый;
          EndIf;
          If Фл Then
            ЗагрузитьГород(ФлНовый,База,ТДеление.Эл,0,Род);
          Else
            ЗагрузитьГород(ФлНовый,База,ТДеление.Эл,0);
          EndIf;
        EndIf;
      EndDo;
      База.Close();
      Message("Добавлено элементов: "+ЧлДобавленных);
      Message("Изменено элементов: "+ЧлИзмененных);
      ФлОк:=1;
    Finally
      If ФлОк Then
        CommitTransaction();
      Else
        RollbackTransaction();
      EndIf;
    EndTry
  EndIf;
  
  оУлицы:=Form.оУлицы.Value;
  If Trim(оУлицы)="" Then
    Box("Не указан путь к файлу STREET.DBF",Q_STOP);
    Exit;
  EndIf;
  
  Message("Загружается справочник адУлицы...");
  База:=Dbf.Create();
  База.Open(оУлицы);
  База.CodePage:=?(Form.о866.Value=1,866,1251);
  
  флДлинаКода:=Struct.Ref("адУлицы").Props("КодБазы").DataType;
  tearStr(флДлинаКода,"STRING.");
  флДлинаКода:=Number(флДлинаКода);
  флИмена:=Form.оИмена.Value;
  
  ФлОк:=0;
  BeginTransaction();
  Try
    ЧлИзмененных:=0;
    ЧлДобавленных:=0;
    ПоследнийЗагруженныйКод:="";
    Чл0:=0;
    База.First();
    База.Select();
    While База.Next() Do
      Чл0:=Чл0+1;
      If Чл0%100=0 Then
        Form.StatusText("Подготовка: "+Чл0);
      EndIf;
    EndDo;
    ВсегоЗаписей:=Чл0;
    
    ТНовыеГорода:=Tab.Create("Город,ФлНовый");
    Чл0:=0;
    База.First();
    База.Select();
    While База.Next() Do
      Чл0:=Чл0+1;
      If Чл0%100=0 Then
        Form.StatusText(""+Чл0+" из "+ВсегоЗаписей);
      EndIf;
      If (ЧлДобавленных+ЧлИзмененных)%500=0 Then
        CommitTransaction();
        BeginTransaction();
      EndIf;
      Код:=LowerCase(Trim(База.CODE));
      If СтрРегион<>"" Then
        If Left(Код,Length(СтрРегион))<>СтрРегион Then
          Continue;
        EndIf;
      EndIf;
      аЭлГ:=Ref.адГорода;
      If аЭлГ.Find("@IsFolder,КодБазы",0,Left(Код,11)) Then
        Имя:=Left(Trim(База.NAME),50);
        ТекущийКод:=Left(База.CODE,Length(База.CODE)-2);
        If ПоследнийЗагруженныйКод=ТекущийКод Then//Две последние цифры -- мусор
          Continue;
        EndIf;
        ПоследнийЗагруженныйКод:=ТекущийКод;
        КодБазы:=Left(Right(База.CODE,флДлинаКода+2),флДлинаКода);
        ТУ:=оТУлиц.GetByName(Trim(LowerCase(База.SOCR)));
        If IsEmpty(ТУ) Then
          ТУ:=оТУлиц.GetByName("0");
        EndIf;
        If not ТНовыеГорода.FindAndGoto(аЭлГ,,"Город",1) Then
          аЭл:=Ref.адУлицы;
          ТНовыеГорода.AddLineSorted("Город","Город,ФлНовый",аЭлГ,аЭл.ElementsNumber("@Parent",аЭлГ)=0);
        EndIf;
        ФлНовый:=ТНовыеГорода.ФлНовый;
        
        Фл:=1;
        аЭл:=Ref.адУлицы;
        If _Or(ФлНовый,not аЭл.Find("@Parent,КодБазы",аЭлГ,КодБазы)) Then
          If _Or(ФлНовый,not аЭл.Find("@Parent,Name,ТипУлицы",аЭлГ,Имя,ТУ)) Then
            аЭл.New();
            аЭл.Parent(аЭлГ);
            ЧлДобавленных:=ЧлДобавленных+1;
            Фл:=0;
          EndIf;
        EndIf;
        If Фл Then
          If _And(аЭл.ТипУлицы=ТУ,флИмена Or(аЭл.Name=Имя),аЭл.Индекс=База.INDEX,аЭл.КодБазы=КодБазы) Then
            Continue;
          EndIf;
          ЧлИзмененных:=ЧлИзмененных+1;
        EndIf;
        аЭл.ТипУлицы:=ТУ;
        If (not флИмена)Or(аЭл.Name="") Then
          аЭл.Name:=Имя;
        EndIf;
        аЭл.Индекс:=База.INDEX;
        аЭл.КодБазы:=КодБазы;
        аЭл.Save();
      EndIf;
    EndDo;
    База.Close();
    Message("Добавлено элементов: "+ЧлДобавленных);
    Message("Изменено элементов: "+ЧлИзмененных);
    ФлОк:=1;
  Finally
    If ФлОк Then
      CommitTransaction();
    Else
      RollbackTransaction();
    EndIf;
  EndTry
  
  Message("Работа окончена!");
EndFUnction
