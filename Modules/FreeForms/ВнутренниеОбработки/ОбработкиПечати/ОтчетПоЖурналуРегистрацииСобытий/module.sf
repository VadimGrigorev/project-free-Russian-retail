//Модуль загружается до загрузки любой формы и остается в памяти до закрытия всех связанных с ним (либо с его потомками) форм.
//Переменные, объявленные с постфиксом export, будут доступны в любой форме или в модулях и формах потомках.
//Модуль может содержать общие функции, используемые формами, или специальные предопределенные функции, вызываемые системой при наступлении разнообразных событий.
//Предопределенные функции имеют свой набор переменных. Дальнейшее описание ищите в документации.
Var Док Export;       //Из выбора документов
Var флРежим Export;   //0 -- обычный, 1 -- выбор документов, 2 -- выбор событий

Function ДобавитьВСписок(aEl,oList)
  If isEmpty(aEl) Then
    Exit;
  EndIf;
  If aEl.DBName()<>Док Then
    Box("Документ "+Str(aEl)+" несовместим с выбранным типом фильтра!",Q_STOP);
    Exit;
  EndIf;
  а:=oList.Find(aEl);
  If а>0 Then
    Box("Документ "+Str(aEl)+" уже был выбран!",Q_STOP);
  Else
    oList.Add(aEl);
  EndIf;  
EndFunction

Function ПриПодбореДокументов(aEl,oList)
  ДобавитьВСписок(aEl,oList);
EndFunction


Function ОбновитьТексты()
  Form.оТипСобытияЧл.isDisabled:=Form.оТипСобытия.Value.SelectedLine<>4;
EndFunction


Function Группировка(З,ТабФорма,Т,Ном,Ур,тГруппы,УрВсего,спОбычные)

  If Ур=УрВсего Then
    оУровни:=глПолучитьУстановку(ТабФорма,"оУровни");
    ИмяПер:="@LINK";
  Else
    If Ном>тГруппы.Size() Then
      Exit;
    EndIf;
    If тГруппы.Check(Ном,"Группировка")=0 Then
      Группировка(З,ТабФорма,Т,Ном+1,Ур,тГруппы,УрВсего,спОбычные);
      Exit;
    EndIf;
    оУровни:=глПолучитьУстановку(ТабФорма,"оУровни");
    ИмяПер:=глПолучитьИмяПеременной(тГруппы,Ном);
  EndIf;
  
  While З.Next(Ур) Do
    пЭл:=З.Get(ИмяПер);
    пЭлР:=пЭл;
    If IsEmpty(пЭл) Then
      пЭл:="< Не выбран >";
    EndIf;
    глОбработатьВыводимоеЗначениеВОтчет(пЭл,ИмяПер);
    пКод:=глПолучитьКод(пЭлР);
    If Ур=УрВсего Then
      //@DATE,@USERNAME,@EVENTTYPE,@EVENTNAME,@LINK,@COMMENT
      пПользователь:=З.Get("@USERNAME");
      пДата:=З.Get("@DATE");
      пТип:=З.Get("@EVENTTYPE");
      пТип:=?(пТип=0,"Визуальное",?(пТип=1,"Программное","Пользовательское, тип: "+пТип));
      пИмя:=З.Get("@EVENTNAME");
      If спОбычные.Find(пИмя)>0 Then
        пИмя:=спОбычные.GetName(спОбычные.Find(пИмя));
      EndIf;
      пОписание:=З.Get("@COMMENT");
      If IsEmpty(пЭлР) Then
        пЭл:="";
      EndIf;
      Т.CopyByX("v2",1);
    Else
      Т.CopyByX("v4",1);
    EndIf;  
    
    НачВыс:=Т.Height();
    Form.StatusText(НачВыс);
    Form.UpdateProgress(-1,,""+Т.Height());
    глРаскраситьСтроку(Т,1,3,Ур,УрВсего,2,8,пЭлР);
    Группировка(З,ТабФорма,Т,Ном+1,Ур+1,тГруппы,УрВсего,спОбычные);

    If (оУровни=1)and(Ур<УрВсего) Then
      глДобавитьУровень(Т,НачВыс,Т.Height());
    EndIf;
  EndDo;
EndFunction


Function Печать(СпУстановки)
  ТабФорма:=СпУстановки.GetByName("Установки");
  Т:=СпУстановки.GetByName("Таблица");
  оГруппы:=глПолучитьУстановку(ТабФорма,"оГруппы");
  НачДата:=BegOfDay(глПолучитьУстановку(ТабФорма,"НачДата"));
  КонДата:=BegOfDay(глПолучитьУстановку(ТабФорма,"КонДата"));
  оУровни:=глПолучитьУстановку(ТабФорма,"оУровни");
  оОб:=глПолучитьУстановку(ТабФорма,"оОб");
  оПлз:=глПолучитьУстановку(ТабФорма,"оПлз");
  оГрп:=глПолучитьУстановку(ТабФорма,"оГрп");
  оТипСобытия:=глПолучитьУстановку(ТабФорма,"оТипСобытия").SelectedLine;
  оТипСобытияЧл:=Number(глПолучитьУстановку(ТабФорма,"оТипСобытияЧл"));
  оСобытие:=глПолучитьУстановку(ТабФорма,"оСобытие");
  оОписание:=глПолучитьУстановку(ТабФорма,"оОписание");
  
  ФлЛок:=0;
  If Т=0 Then
    If not глПроверитьДатуВОтчетах(Param,Form.НачДата.Value,Form.КонДата.Value,-2) Then
      Exit;
    EndIf;
    Т:=Table.Create();
    НачДата:=BegOfDay(Form.НачДата.Value);
    КонДата:=BegOfDay(Form.КонДата.Value);
  Else  
    ФлЛок:=1;
    Т.Lock();
    Т.Clear();
  EndIf;  
  
  Try
    КонДата:=КонДата+1;
    Стр1:="";
    Стр2:="";
    If оОб.Get(1).Size()>0 Then
      Стр1:=Стр1+?(Стр1="","",",")+"->@LINK";
      Стр2:=Стр2+?(Стр2="","",",")+"оОб.Get(1)";
    EndIf;
    If оОб.Get(2).Size()>0 Then
      Стр1:=Стр1+?(Стр1="","",",")+"!->@LINK";
      Стр2:=Стр2+?(Стр2="","",",")+"оОб.Get(2)";
    EndIf;
    оПлз1:=List.Create();
    оПлз2:=List.Create();
    If оПлз.Get(1).Size()>0 Then
      aList:=Ref.общПользователи.Load(0,"@IsFolder,->@ELEMENT",0,оПлз.Get(1));
      For i:=1 To aList.Size() Do
        аСтр:=aList.Get(i).Идентификатор;
        If оПлз1.Find(аСтр)=0 Then
          оПлз1.Add(аСтр);
        EndIf;
      EndDo;
      Стр1:=Стр1+?(Стр1="","",",")+"=@USERNAME";
      Стр2:=Стр2+?(Стр2="","",",")+"оПлз1";
    EndIf;
    If оПлз.Get(2).Size()>0 Then
      aList:=Ref.общПользователи.Load(0,"@IsFolder,->@ELEMENT",0,оПлз.Get(2));
      For i:=1 To aList.Size() Do
        аСтр:=aList.Get(i).Идентификатор;
        If оПлз2.Find(аСтр)=0 Then
          оПлз2.Add(аСтр);
        EndIf;
      EndDo;
      Стр1:=Стр1+?(Стр1="","",",")+"<>@USERNAME";
      Стр2:=Стр2+?(Стр2="","",",")+"оПлз2";
    EndIf;
    If оТипСобытия>1 Then
      If оТипСобытия<4 Then
        Стр1:=Стр1+?(Стр1="","",",")+"@EVENTTYPE";
        Стр2:=Стр2+?(Стр2="","",",")+"оТипСобытия-2";
      Else
        Стр1:=Стр1+?(Стр1="","",",")+"@EVENTTYPE";
        Стр2:=Стр2+?(Стр2="","",",")+"оТипСобытияЧл";
      EndIf;
    EndIf;
    If оСобытие.Get(1).Size()>0 Then
      Стр1:=Стр1+?(Стр1="","",",")+"@EVENTNAME";
      Стр2:=Стр2+?(Стр2="","",",")+"оСобытие.Get(1)";
    EndIf;
    If not IsEmpty(оОписание) Then
      Стр1:=Стр1+?(Стр1="","",",")+"%@COMMENT";
      Стр2:=Стр2+?(Стр2="","",",")+"оОписание";
    EndIf;
    
    If Стр1="" Then
      aTab:=Eval("Logs.Query(НачДата,КонДата)");
    Else
      aTab:=Eval("Logs.Query(НачДата,КонДата,,"""+Стр1+""","+Стр2+")");
    EndIf;
    If aTab.SizeC()=0 Then
      aTab:=Tab.Create("@DATE,@USERNAME,@EVENTTYPE,@EVENTNAME,@LINK,@COMMENT,@ELEMENT");
    EndIf;
    пЧисло:=aTab.Size();
    //aTab: @DATE,@USERNAME,@EVENTTYPE,@EVENTNAME,@LINK,@COMMENT
    
    СтрГруппы:="";
    Фл:=0;
    оГруппы.Select();
    While оГруппы.Next() Do
      If оГруппы.Check(оГруппы.CurLine,"Группировка")=0 Then
        Continue;
      EndIf;
      Стр:=оГруппы.Путь;
      tearStr(Стр,"#");
      СтрГруппы:=?(СтрГруппы="","",СтрГруппы+",")+Стр;
      If pos("@Месяц",оГруппы.Путь)>0 Then
        aTab.AddColumn("Месяц");
        Фл:=Фл or 2;
      ElseIf pos("@Год",оГруппы.Путь)>0 Then
        aTab.AddColumn("Год");
        Фл:=Фл or 4;
      ElseIf pos("@День",оГруппы.Путь)>0 Then
        aTab.AddColumn("День");
        Фл:=Фл or 1;
      ElseIf Стр="@LINK" Then
        If оГрп=1 Then
          ТипСтр:="";
          aTab.Select();
          While aTab.Next() Do
            aEl:=aTab.Get(aTab.CurLine,"@LINK");
            if not isEmpty(aEl) Then
              ТипСтр2:=TypeStr(aEl);//DB.REF.<Name>
              if pos("DB.REF.",ТипСтр2)<>1 Then
                ТипСтр:="";
                Break;
              EndIf;
              If aEl.IsFolder() Then
                ТипСтр:="";
                Break;
              EndIf;
              If _And(ТипСтр<>"",ТипСтр<>ТипСтр2) Then
                ТипСтр:="";
                Break;
              EndIf;
              ТипСтр:=ТипСтр2;
            EndIf;
          EndDo;
          If ТипСтр<>"" Then
            TearStr(ТипСтр,"DB.REF.");
            СтрГруппы:=СтрГруппы+" with folders Ref."+ТипСтр;
          EndIf;  
        EndIf;
      EndIf;
    EndDo;
    СтрГруппы:=?(СтрГруппы="","",СтрГруппы+",")+"@ELEMENT";
    
    aTab.Select();
    While aTab.Next() Do
      Дата:=aTab.Get(aTab.CurLine,"@DATE");
      aTab["@ELEMENT"]:=FormatDate(Дата,"YYYYmmddhhttssp")+aTab["@ELEMENT"];//Сортировка по дате потом по коду элемента. Необходима, так как иногда более старые записи попадают вперед
      If Фл And 1<>0 Then
        aTab.День:=BegOfDay(Дата);
      EndIf;  
      If Фл And 2<>0 Then
        aTab.Месяц:=BegOfMonth(Дата);
      EndIf;  
      If Фл And 4<>0 Then
        aTab.Год:=BegOfYear(Дата);
      EndIf;  
    EndDo;
    З:=Query.CreateFromTable(aTab,СтрГруппы,"");
    
    УрВсего:=глКоличествоУровнейИзФильтров(оГруппы);
  
    оЗагол:="Отчет по журналу регистрации событий";
    оСвойства:=глСвойстваПечатиСложные(ТабФорма,НачДата,КонДата,"оОб,оПлз,оСобытие",?((оТипСобытия>1)and(оТипСобытия<4),"Тип события@оТипСобытия",""),"");
    If оТипСобытия>3 Then
      оСвойства:=оСвойства+_NEWLINE+"Тип события: код "+оТипСобытияЧл;
    EndIf;
    If not IsEmpty(оОписание) Then
      оСвойства:=оСвойства+_NEWLINE+"По фразе в описании: "+оОписание;
    EndIf;

    
    Сп:=СпУстановки;
    Т.CopyByX("v1",1);
    Т.Options.FixedLine:=Т.Height();
    спОбычные:=List.Create();
    спОбычные.Add("NEW","Новый объект");
    спОбычные.Add("WRITE","Запись объекта");
    спОбычные.Add("OPEN","Просмотр объекта");
    спОбычные.Add("MARK","Удаление объекта");
    спОбычные.Add("UNMARK","Отмена удаления объекта");
    спОбычные.Add("DELETE","Физическое удаление объекта");
    спОбычные.Add("PUBLISH","Обработка объекта");
    спОбычные.Add("UNPUBLISH","Отмена обработки объекта");
    спОбычные.Add("LOGIN","Вход в систему");
    спОбычные.Add("LOGOUT","Выход из системы");
    спОбычные.Add("USER","Событие задано администратором");
    спОбычные.Add("REPORT","Формирование отчета");
    спОбычные.Add("НАРУШЕНИЯ","Вероятные нарушения в порядке работы");
    
    
    Группировка(З,ТабФорма,Т,1,1,оГруппы,УрВсего+1,спОбычные);
    Т.CopyByX("v3",1);
    
    If оУровни=1 Then
      Т.Levels().Close();
    EndIf;
    глПечатнаяФормаОтчета(Т,Param,оЗагол);
  Finally
    If ФлЛок Then
      Т.Unlock();
      Т.EditorMode:=1;
    EndIf;
  EndTry;    
  If not ФлЛок then 
    глЭтоБыстрыйВызовОтчета(Param,1);
  endif;  
EndFunction


Function OnDoubleClick(Таб)
  Result:=not глРаботаСОсобымиИконкамиТаблицы(Таб);
EndFunction

Function OnExecute(СпУстановки)
  If флРежим=1 Then
    aList:=Param.GetByName("Фильтр");
    aList.Get(1).Clear();
    Form.oList1.Value.CopyTo(aList.Get(1));
    Form.Close(0);
    Exit;
  ElseIf флРежим=2 Then
    aList:=Param.GetByName("Фильтр");
    aList.Clear();
    oList1:=Form.oList1.Value;
    For i:=1 To oList1.Size() Do
      If oList1.Check(i)>0 Then
        aList.Add(oList1.Get(i));
      EndIf;
    EndDo;
    Form.Close(0);
    Exit;
  EndIf;
  
  Печать(СпУстановки);
EndFunction


