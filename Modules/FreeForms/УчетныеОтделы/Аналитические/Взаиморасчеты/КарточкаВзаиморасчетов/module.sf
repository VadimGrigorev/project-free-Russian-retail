//Модуль загружается до загрузки любой формы и остается в памяти до закрытия всех связанных с ним (либо с его потомками) форм.
//Переменные, объявленные с постфиксом export, будут доступны в любой форме или в модулях и формах потомках.
//Модуль может содержать общие функции, используемые формами, или специальные предопределенные функции, вызываемые системой при наступлении разнообразных событий.
//Предопределенные функции имеют свой набор переменных. Дальнейшее описание ищите в документации.
Var аТипУчета Export;


Function ПолучитьТЗ(флБезДокументов,оВключить,оТипДолга,оТипДокумента,оЮЛ,оН,оК,оД)
    ТЗ:="Period From НачДата to КонДата;"+_NEWLINE;
    If оВключить.SelectedLine>1 Then
      ТЗ:=ТЗ+"зВкл:=Storage.Взаиморасчеты.УчитыватьВНалоговомУчете;"+?(оВключить.SelectedLine=2,"Condition(зВкл>0);","Condition(зВкл=0);")+_NEWLINE;
    EndIf;
    If оТипДолга.SelectedLine>1 Then
      ТЗ:=ТЗ+"зПП:=Storage.Взаиморасчеты.ВидДолга;"+?(оТипДолга.SelectedLine=2,"Condition(зПП=ппПокупатель);","Condition(зПП=ппПоставщик);")+_NEWLINE;
    EndIf;
    If оТипДокумента.SelectedLine>1 Then
      Чл:=оТипДокумента.Get(оТипДокумента.SelectedLine);
      ТЗ:=ТЗ+"зТД:=Storage.Взаиморасчеты.ТипДокумента;Condition(зТД="+Чл+");"+_NEWLINE;
    EndIf;
    ТЗ:=ТЗ+"зЮЛ:=Storage.Взаиморасчеты.Договор.ЮрЛицо;
    |зК:=Storage.Взаиморасчеты.Договор.@Parent;
    |зД:=Storage.Взаиморасчеты.Договор;
    |зН:=Storage.Взаиморасчеты.Тара;
    |зДок:=Storage.Взаиморасчеты.@LINK;"+_NEWLINE+
    ?(оЮЛ.Get(1).Size()>0,"Condition(зЮЛ IN оЮЛ.Get(1));","")+_NEWLINE+
    ?(оЮЛ.Get(2).Size()>0,"Condition(зЮЛ NOT IN оЮЛ.Get(2));","")+_NEWLINE+
    ?(оН.Get(1).Size()>0,"Condition(зН IN оН.Get(1));","")+_NEWLINE+
    ?(оН.Get(2).Size()>0,"Condition(зН NOT IN оН.Get(2));","")+_NEWLINE+
    ?(оК.Selected()<>0,"Condition(зК IN оК);","")+_NEWLINE+
    ?(not isEmpty(оД),"Condition(зД IN оД);","")+_NEWLINE+
    "зРасход:=Expense(Сумма);
    |зНачОст:=BegTotals(Сумма);
    |зПриход:=Income(Сумма);
    |зКонОст:=EndTotals(Сумма);
    |зРасходКол:=Expense(Количество);
    |зНачОстКол:=BegTotals(Количество);
    |зПриходКол:=Income(Количество);
    |зКонОстКол:=EndTotals(Количество);"
    +?(not флБезДокументов,"Group зДок;","")+
    "Group зН;";  
    
    Exit ТЗ;
EndFunction


Function Печать(СпУстановки)
  ТабФорма:=СпУстановки.GetByName("Установки");
  Т:=СпУстановки.GetByName("Таблица");
  оГруппы:=глПолучитьУстановку(ТабФорма,"оГруппы");
  НачДата:=BegOfDay(глПолучитьУстановку(ТабФорма,"НачДата"));
  КонДата:=BegOfDay(глПолучитьУстановку(ТабФорма,"КонДата"));
  оК:=глПолучитьУстановку(ТабФорма,"оК");
  If isEmpty(оК) Then
    Box("Выберите контрагента!",Q_STOP);
    Exit;  
  EndIf;
  
  ппПокупатель:=Enum.ПокупательПоставщик.Покупатель;
  ппПоставщик:=Enum.ПокупательПоставщик.Поставщик;  
  НеСтирать:=глПолучитьУстановку(ТабФорма,"НеСтирать");

  
  ФлЛок:=0;
  If Т=0 Then
    If not глПроверитьДатуВОтчетах(Param,Form.НачДата.Value,Form.КонДата.Value,0) Then
      Exit;
    EndIf;
    Т:=Table.Create();
    НачДата:=BegOfDay(Form.НачДата.Value);
    КонДата:=BegOfDay(Form.КонДата.Value);
    оЮЛ:=глПолучитьУстановку(ТабФорма,"оЮЛ");
    оД:=глПолучитьУстановку(ТабФорма,"оД");
  Else  
    ФлЛок:=1;
    Т.Lock();
    If НеСтирать<>1 Then
      Т.Clear();
    EndIf;  
  EndIf;  
  
  Try
    оВключить:=глПолучитьУстановку(ТабФорма,"оВключить");
    оТипДолга:=глПолучитьУстановку(ТабФорма,"оТипДолга");
    оТипДокумента:=глПолучитьУстановку(ТабФорма,"оТипДокумента");
    оЮЛ:=глПолучитьУстановку(ТабФорма,"оЮЛ");
    оД:=глПолучитьУстановку(ТабФорма,"оД");
    оН:=глПолучитьУстановку(ТабФорма,"оН");
    оВход:=глПолучитьУстановку(ТабФорма,"оВход");
    оАдр:=глПолучитьУстановку(ТабФорма,"оАдр");
    оДни:=глПолучитьУстановку(ТабФорма,"оДни");
  
    ТЗ:=ПолучитьТЗ(1,оВключить,оТипДолга,оТипДокумента,оЮЛ,оН,оК,оД);
    ЗТр:=Query.Create();
    ЗТр.Execute(ТЗ);

    ТТара:=Tab.Create("зН,Сумма,Количество,См0,Кл0,См1,Кл1");
    While ЗТр.Next(1) Do
      зН:=?(isEmpty(ЗТр.зН),0,ЗТр.зН);
      ТТара.AddLine("зН,Сумма,Количество",зН,ЗТр.зНачОст,ЗТр.зНачОстКол);
    EndDo;
    If ТТара.Find(0,,"зН")=0 Then
      ТТара.AddLine("зН,Сумма,Количество",0,0,0);
    EndIf;
    ТТара.Sort("зН");
    
    ТЗ:=ПолучитьТЗ(0,оВключить,оТипДолга,оТипДокумента,оЮЛ,оН,оК,оД);
    З:=Query.Create();
    З.Execute(ТЗ);
    
    оЗагол:="Карточка взаиморасчетов с контрагентом "+Trim(оК.ПолнНаименование)+_NEWLINE+"(Код: "+оК.Code+")";    
    оСвойства:=глСвойстваПечатиСложные(ТабФорма,НачДата,КонДата,"оЮЛ,оК,оД,оН",?(оВключить.SelectedLine>1,"Включать в книги п/п@оВключить","")+
      ?(оТипДолга.SelectedLine>1,",Тип долга@оТипДолга","")+?(оТипДокумента.SelectedLine>1,",Тип документа@оТипДокумента",""));
    
    Сп:=СпУстановки;
    
    Т.CopyByX("v1|h1",1);
    For i:=1 to ТТара.Size() Do
      ТТара.CurLine:=i;
      If ТТара.зН<>0 Then
        Т.CopyByX("v1|h2");
      EndIf;  
    EndDo;
    Т.CopyByX("v1|h3");
    Т.Area(2,Т.Height()-1,7+ТТара.Size()-1,Т.Height()-1).Merge();
    
    Т.CopyByX("v2|h1",1);
    For i:=1 to ТТара.Size() Do
      ТТара.CurLine:=i;
      If ТТара.зН<>0 Then
        пТара:=ТТара.зН;
        Т.CopyByX("v2|h2");
      EndIf;  
    EndDo;
    If ТТара.Size()>2 Then
      Т.Area(6,Т.Height()-1,5+ТТара.Size()-1,Т.Height()-1).Merge();
    EndIf;
    Т.CopyByX("v2|h3");
    If НеСтирать<>1 Then
      Т.Options.FixedLine:=Т.Height();
    EndIf;  
    
    //Начальное сальдо.
    ЗТр.First();
    пСальдо:="Сальдо на начало периода:";
    пТвКон0:=ЗТр.BegTotals("зНачОст");
    пТвКон:=глФРМ(пТвКон0);
    пТвР:="";
    пТвП:="";
    пТв:=глФРМ(Number(ТТара.FindAndGet("Сумма",0,,"зН")));
    Т.CopyByX("v4|h1",1);
    For i:=1 to ТТара.Size() Do
      ТТара.CurLine:=i;
      If ТТара.зН<>0 Then
        пТр:=глФРМ(Number(ТТара.Сумма));
        пТрК:=глФРМЧл(Number(ТТара.Количество));
        Т.CopyByX("v4|h2");
      EndIf;  
    EndDo;
    Т.CopyByX("v4|h3");    

    
    Выс:=0;
    День:=0;
    Цвет:=0;
    //Строки таблицы
    While З.Next(1) Do
      
      пДок:=З.зДок;
      If IsEmpty(пДок) Then
        Continue;
      Endif;
      пДокР:=пДок;
      пДок:=""+пДок.КодОперации+" №"+пДок.DocNum;
      
      If оВход=1 Then
        If глЕстьРеквизитДокумента("НомерДокВходящий",пДокР.DBName()) Then
          пДок:=пДок+" (вх. №"+Trim(пДокР.НомерДокВходящий)+" от "+пДокР.ДатаДокВходящий+")";
        EndIf;
      EndIf;
      пДата:=FormatDate(пДокР.DocDate,"dd.mm.YYYY hh:tt:ss");
      
      пАдр:="Кол:";
      пАдрР:=GetNothing();
      If оАдр=1 Then
        If глЕстьРеквизитДокумента("АдресДоставки",пДокР.DBName()) Then
          пАдр:=Trim(пДокР.АдресДоставки);
        Else
          пАдр:="";
        EndIf;
      EndIf;
      
      If оДни=1 Then
        If День<>0 Then
          If День<>BegOfDay(пДокР.DocDate) Then
            День:=BegOfDay(пДокР.DocDate);
            Цвет:=1-Цвет;
            If Цвет=0 Then
              Т.Area(2,Выс+1,4+ТТара.Size()+3,Т.Height()).BackgroundColorRGB:=MixColors(_CLR_WINDOW,_CLR_YELLOW,15);
            EndIf;
            Выс:=Т.Height();
          EndIf;
        Else
          Выс:=Т.Height();
          День:=BegOfDay(пДокР.DocDate);
        EndIf;
      EndIf;
      
      пТвП:=глФРМ(З.зПриход);
      пТвР:=глФРМ(З.зРасход);
      пТвКон0:=пТвКон0+З.зПриход-З.зРасход;
      пТвКон:=глФРМ(пТвКон0);
      ТТара.Select();
      While ТТара.Next() Do
        ТТара.См1:=0;
        ТТара.Кл1:=0;
      EndDo;
      
      While З.Next(2) Do
        зН:=?(isEmpty(З.зН),0,З.зН);
        If ТТара.FindAndGoto(зН,,"зН")>0 Then
          ТТара.См1:=З.зПриход-З.зРасход;
          ТТара.Кл1:=З.зПриходКол-З.зРасходКол;
          ТТара.См0:=Number(ТТара.См0)+ТТара.См1;
          ТТара.Кл0:=Number(ТТара.Кл0)+ТТара.Кл1;
        EndIf;
      EndDo;
      
      пТв:=глФРМ(Number(ТТара.FindAndGet("См1",0,,"зН")));
      Т.CopyByX("v3|h1",1);
      For i:=1 to ТТара.Size() Do
        ТТара.CurLine:=i;
        If ТТара.зН<>0 Then
          пТр:=глФРМ(Number(ТТара.См1));
          пТрК:=глФРМЧл(Number(ТТара.Кл1));
          Т.CopyByX("v3|h2");
        EndIf;  
      EndDo;
      Т.CopyByX("v3|h3");
      Form.StatusText(Т.Height());
    EndDo;    
        
    If оДни=1 Then
      If День<>0 Then
        Цвет:=1-Цвет;
        If Цвет=0 Then
          Т.Area(2,Выс+1,4+ТТара.Size()+3,Т.Height()).BackgroundColorRGB:=MixColors(_CLR_WINDOW,_CLR_YELLOW,15);
        EndIf;
        Выс:=Т.Height();
      EndIf;
    EndIf;
    
    
    //Обороты за период.
    пПер:=Str(НачДата)+" по "+Str(КонДата);
    пТвР:=глФРМ(ЗТр.Compute("зРасход"));
    пТвП:=глФРМ(ЗТр.Compute("зПриход"));
    пТв=глФРМ(Number(ТТара.FindAndGet("См0",0,,"зН")));
    Т.CopyByX("v5|h1",1);
    For i:=1 to ТТара.Size() Do
      ТТара.CurLine:=i;
      If ТТара.зН<>0 Then
        пТр:=глФРМ(Number(ТТара.См0));
        пТрК:=глФРМЧл(Number(ТТара.Кл0));
        Т.CopyByX("v5|h2");
      EndIf;  
    EndDo;
    Т.CopyByX("v5|h3");    
    
    
    //Конечное сальдо.
    пСальдо:="Сальдо на конец периода:";
    пТвКон:=глФРМ(пТвКон0);
    пТвР:="";
    пТвП:="";
    пТв:=глФРМ(Number(ТТара.FindAndGet("См0",0,,"зН"))+Number(ТТара.FindAndGet("Сумма",0,,"зН")));
    Т.CopyByX("v4|h1",1);
    For i:=1 to ТТара.Size() Do
      ТТара.CurLine:=i;
      If ТТара.зН<>0 Then
        пТр:=глФРМ(Number(ТТара.См0)+Number(ТТара.Сумма));
        пТрК:=глФРМЧл(Number(ТТара.Кл0)+Number(ТТара.Количество));
        Т.CopyByX("v4|h2");
      EndIf;  
    EndDo;
    Т.CopyByX("v4|h3");    
    
    глПечатнаяФормаОтчета(Т,Param,"Карточка взаиморасчетов с контрагентом "+Trim(оК.ПолнНаименование));
    
    If not ФлЛок Then
      глФормированиеОтчетаДобавитьВСобытия("Карточка взаиморасчетов с контрагентом",НачДата,КонДата,"оЮЛ,оК,оД,оН");
    EndIf;
  Finally
    If ФлЛок Then
      Т.Unlock();
      Т.EditorMode:=1;
    EndIf;
  EndTry;
  If not ФлЛок then 
    глЭтоБыстрыйВызовОтчета(Param,1);
  endif;  
EndFunction
  

Function OnDoubleClick(Таб)
  result:=not глРаботаСОсобымиИконкамиТаблицы(Таб);
EndFunction



Function OnExecute(СпУстановки)
  Печать(СпУстановки);
EndFunction


