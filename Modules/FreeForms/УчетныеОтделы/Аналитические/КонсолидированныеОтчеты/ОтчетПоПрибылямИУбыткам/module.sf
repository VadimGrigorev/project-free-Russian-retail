//Модуль загружается до загрузки любой формы и остается в памяти до закрытия всех связанных с ним (либо с его потомками) форм.
//Переменные, объявленные с постфиксом export, будут доступны в любой форме или в модулях и формах потомках.
//Модуль может содержать общие функции, используемые формами, или специальные предопределенные функции, вызываемые системой при наступлении разнообразных событий.
//Предопределенные функции имеют свой набор переменных. Дальнейшее описание ищите в документации.

Var Баланс Export;                                    //Список итогов
Var ТаблицаЗатрат Export;                             //Накопление затрат по индексам цз/сз
Var ТПродОбщ Export,ТбМарж Export,ТбМарж2 Export;     //Общая таблица продаж за период, таблица марж. прибыли, табл. марж прибыли поступлений
Var ПродОтвет Export;                                 //Вычисленное значение продаж по индексу и ресурсу
Var кфНДС Export;                                     //Быстрая разница для НДС (продаж и поступлений)
Var ФлСПоставщикамиС Export;                          //Учистывать поставщиков С в продажах
Var НачДата Export, КонДата Export;
Var оНДС Export;
Var Т Export;

Function ТаблицаГоризонтальногоРазворота(Заполнить=0)
  Тб:=Tab.Create("Эл,ЭлР,Тб");
  Тб.AddColumn("См",,"Number");
  If Заполнить Then
    Тб.NewLine();
  EndIf;
  Exit Тб;
EndFunction

Function ПодсчитатьРекурсивно(Тб,КолВоГр,ДеньНом)
  If КолВоГр>0 Then
    См:=0;
    Тб.Select();
    While Тб.Next() Do
      См:=См+ПодсчитатьРекурсивно(Тб.Тб,КолВоГр-1,ДеньНом);
    EndDo;
    Exit См;
  EndIf;
  Exit Тб.Get(ДеньНом,"См");
EndFunction

Function ВывестиГруппировкуПростую(Актив,пИмя,пИмяР,Тб,КолВоГр,НГр=0,Верхний=1,Запрет=0)
  If IsEmpty(пИмя) Then
    пИмя:="< Не выбран >";
  EndIf;
  If Актив="А" Then
    Актив:=2;
  ElseIf Актив="П" Then
    Актив:=5;
  ElseIf Актив="О" Then
    Актив:=8;
  EndIf;
  оУровни:=Form.оУровни.Value;
  
  If _And(IsDBObject(пИмяР),пИмяР.DBName()="зтЦентрыЗатрат",пИмяР.ЗапретПросмотра) Then
    If not глПользователь.бухГлавныйБухгалтер Then
      Запрет:=1;
    EndIf;
  EndIf;
  //Подсчет верхней 
  пЗн:=ПодсчитатьРекурсивно(Тб,КолВоГр,1);
  If _And(Round(пЗн,6)=0,КолВоГр=0) Then
    Exit;
  EndIf;
  If (Верхний=1)And(Актив<>8) Then
    Баланс:=Баланс+пЗн;
  EndIf;
  пЗн:=глФРМ(?(Актив=5,-пЗн,пЗн));
  If Запрет=0 Then
    Т.CopyByX("v"+(Актив+НГр)+"|h1",1);
    Т.CopyByX("v"+(Актив+НГр)+"|h2");
    Т.CopyByX("v"+(Актив+НГр)+"|h3");
  EndIf;
  If КолВоГр=0 Then
    Exit;
  EndIf;
  НачВыс:=Т.Height();
  //Вывод младших
  Тб.Select();
  While Тб.Next() Do
    ВывестиГруппировкуПростую(Актив,Тб.Эл,Тб.ЭлР,Тб.Тб,КолВоГр-1,НГр+1,0,Запрет);
  EndDo;
  If оУровни=1 Then
    глДобавитьУровень(Т,НачВыс,Т.Height());
  EndIf;  
EndFunction




Function РасчетПолнойТаблицыПродаж()
  //Индекс: 0@0 (не используется)
  ТПродОбщ:=Tab.Create("Индекс,ИндексПр,ИндексФл");
  ТПродОбщ.AddColumn("ТНом1",,"Number"); //Продажи в рублях
  ТПродОбщ.AddColumn("ТНом2",,"Number"); //Объем в литрах
  ТПродОбщ.AddColumn("ТНом3",,"Number"); //Количество в упаковках
  ТПродОбщ.AddColumn("ТНом4",,"Number"); //Количество
  ТПродОбщ.AddColumn("ТНом5",,"Number"); //Масса нетто в кг
  ТПродОбщ.AddColumn("ТНом7",,"Number"); //Прибыль
  оВО:=Form.оВО.Value.SelectedLine;
  
  //Марж. прибыль продаж
  ТбМарж:=ТаблицаГоризонтальногоРазворота();
  
  Form.StatusText("Общий запрос по продажам...");
  ТЗ:="Period From НачДата to КонДата;
  |зМрТип:=Storage.ДвижениеТМЦ.ТипЗаписи;Condition(зМрТип<2);
  |зСк:=Storage.ДвижениеТМЦ.Склад;
  |зСкА:=Storage.ДвижениеТМЦ.Склад.ИспользоватьВАктиве;Condition(зСкА=1);
  |зМрТУ:=Storage.ДвижениеТМЦ.ТипУчета;Condition(зМрТУ=0);"+
  ?(ФлСПоставщикамиС=0,"зТипАкт:=Storage.ДвижениеТМЦ.Договор.@Parent.ТипАктива;Condition(зТипАкт<>3);","")+
  "зН:=Storage.ДвижениеТМЦ.Номенклатура;
  |зТГ:=Storage.ДвижениеТМЦ.Номенклатура.ТоварнаяГруппа;
  |зСмМ:=SUM(Storage.ДвижениеТМЦ.Себестоимость);
  |зНом1:=SUM(Storage.ДвижениеТМЦ.Сумма);
  |зК:=Storage.ДвижениеТМЦ.Количество;зНом4:=SUM(зК);
  |зО:=Storage.ДвижениеТМЦ.Номенклатура.ОбъемЛитров;зНом2:=SUM(зК*зО);
  |зКО:=Storage.ДвижениеТМЦ.Номенклатура.оснКоэффициент;зНом3:=SUM(зК/?(зКО=0,1,зКО));
  |зВес0:=Storage.ДвижениеТМЦ.Номенклатура.базМассаНетто;зНом5:=SUM(зК*зВес0);
  |Group зТГ,зСк;"+
  ?(оНДС=1,"зНДС:=Storage.ДвижениеТМЦ.СтавкаНДС;Group зНДС;");
  
  З:=Query.Create();
  З.Execute(ТЗ);
  Form.StatusText("Расчет общей таблицы продаж...");
  
  ТПродОбщ.AddLine("Индекс,ИндексПр,ИндексФл","0@0",0,0);
  While З.Next(1) Do
    While З.Next(2) Do
      ТПродОбщ.ТНом1:=ТПродОбщ.ТНом1+З.зНом1;
      ТПродОбщ.ТНом2:=ТПродОбщ.ТНом2+З.зНом2;
      ТПродОбщ.ТНом3:=ТПродОбщ.ТНом3+З.зНом3;
      ТПродОбщ.ТНом4:=ТПродОбщ.ТНом4+З.зНом4;
      ТПродОбщ.ТНом5:=ТПродОбщ.ТНом5+З.зНом5;
      ТПродОбщ.ТНом4:=ТПродОбщ.ТНом4+З.зНом4;
      //Марж прибыль
      If оНДС=0 Then
        СмМарж:=З.зНом1-З.зСмМ;
      Else
        СмМарж:=0;
        While З.Next(3) Do
          СмМарж:=СмМарж+(З.зНом1-З.зСмМ)*кфНДС[Max(З.зНДС.Index(),1)];
        EndDo;
      EndIf;
      ТПродОбщ.ТНом7:=ТПродОбщ.ТНом7+СмМарж;
      If ТбМарж.FindAndGoto(З.зТГ,,"Эл")=0 Then
        ТбМарж.AddLine("Эл,ЭлР,Тб",З.зТГ,З.зТГ,ТаблицаГоризонтальногоРазворота());
        ТбМарж.Тб.NewLine();
      EndIf;
      Тб2:=ТбМарж.Тб;
      Тб2.CurLine:=1;
      Тб2.См:=Тб2.См+СмМарж;
    EndDo;
  EndDo;
  
  
  //Марж. прибыль поступлений
  ФлКомпенсация:=(Number(DBVar.ИспользоватьБлокМаркетинга))And(оВО>1);
  ТбМарж2:=ТаблицаГоризонтальногоРазворота();
  ТЗ:="Period From НачДата to КонДата;
  |зСк:=Storage.ДвижениеТМЦ.Склад;
  |зСкА:=Storage.ДвижениеТМЦ.Склад.ИспользоватьВАктиве;Condition(зСкА=1);
  |зН:=Storage.ДвижениеТМЦ.Номенклатура;
  |зТГ:=Storage.ДвижениеТМЦ.Номенклатура.ТоварнаяГруппа;
  |зМрТип:=Storage.ДвижениеТМЦ.ТипЗаписи;
  |зМрТУ:=Storage.ДвижениеТМЦ.ТипУчета;Condition(зМрТУ=0);
  |зТипАкт:=Storage.ДвижениеТМЦ.Договор.@Parent.ТипАктива;"+
  ?(ФлСПоставщикамиС=0,"Condition(зТипАкт<>3);")+  
  "Condition(зМрТип>1);Condition(зМрТип<4);
  |зС00:=Storage.ДвижениеТМЦ.Себестоимость;
  |зС0:=Storage.ДвижениеТМЦ.Сумма;"+
  ?(ФлКомпенсация=0,"зСмП:=SUM(зС00);","зСмП:=SUM(?(зТипАкт<>1,зС00,0));")+
  ?(ФлКомпенсация=0,"зСмМ:=SUM(зС0);","зСмМ:=SUM(?(зТипАкт<>1,зС0,0));")+
  "Group зТГ,зСк;"+
  ?(оНДС=0,"","зНДС:=Storage.ДвижениеТМЦ.СтавкаНДС;Group зНДС;");
    
  Form.StatusText("Общий запрос по прибыли поступлений...");
  З:=Query.Create();
  З.Execute(ТЗ);
  
  ТПродОбщ.CurLine:=1;
  While З.Next(1) Do
    While З.Next(2) Do
      If оНДС=0 Then
        СмМарж:=З.зСмП-З.зСмМ;
      Else
        СмМарж:=0;
        While З.Next(3) Do
          СмМарж:=СмМарж+(З.зСмП-З.зСмМ)*кфНДС[Max(З.зНДС.Index(),1)];
        EndDo;
      EndIf;
      ТПродОбщ.ТНом7:=ТПродОбщ.ТНом7+СмМарж;
      
      If ТбМарж2.FindAndGoto(З.зТГ,,"Эл")=0 Then
        ТбМарж2.AddLine("Эл,ЭлР,Тб",З.зТГ,З.зТГ,ТаблицаГоризонтальногоРазворота());
        ТбМарж2.Тб.NewLine();
      EndIf;
      Тб2:=ТбМарж2.Тб;
      Тб2.CurLine:=1;
      Тб2.См:=Тб2.См+СмМарж;
    EndDo;
  EndDo;
  
  Form.StatusText("");
  Exit 1;
EndFunction

Function ПолучитьТаблицуОсобыхСкидокПоставщиков(Тип="Expense")
  ТЗ2:="Period From НачДата To КонДата;
  |зСкТип:=Storage.СкидкиПоставщиков.СтатьяМаркетинга.НеКомпенсируемаяСтатья;Condition(зСкТип=0);"+
  ?(ФлСПоставщикамиС=0,"зТипАкт:=Storage.СкидкиПоставщиков.Поставщик.ТипАктива;Condition(зТипАкт<>3);")+
  "зК:=Storage.СкидкиПоставщиков.Поставщик;
  |зСм:="+Тип+"("+?(оНДС=0,"Сумма","СуммаБезНДС")+");"+
  "Group зК;";
  Form.StatusText("Запрос по скидкам поставщиков ("+Тип+")...");
  З:=Query.Create();
  З.Execute(ТЗ2);
  ТбМинус:=Tab.Create("Индекс");
  ТбМинус.AddColumn("См",,"Number");
  While З.Next(1) Do
    If not ТбМинус.FindAndGoto(З.зК,,"Индекс") Then
      ТбМинус.AddLine("Индекс",З.зК);
    EndIf;
    ТбМинус.См:=ТбМинус.См+З.зСм;
  EndDo;
  Exit ТбМинус;
EndFunction

Function ПолучитьМаржПрибыльОстатков()
  Form.StatusText("Проверка изменения цен номенклатуры...");
  пТовар:=Enum.ВидыНоменклатуры.Товар;
  ТНом:=глПолучитьПромежуточнуюТаблицуИзмененияЦенНоменклатуры(НачДата,КонДата,List.Create(List.Create(),List.Create()),List.Create(List.Create(),List.Create()));
  If ТНом=0 Then
    Exit;
  EndIf;
  
  Form.StatusText("Запрос по маржинальной прибыли остатков...");
  ТЗ:="Period From НачДата To КонДата;
  |зСк:=Storage.ОстаткиТМЦ.Склад;
  |зСА:=Storage.ОстаткиТМЦ.Склад.ИспользоватьВАктиве;
  |Condition(зСА=1);
  |зН:=Storage.ОстаткиТМЦ.Номенклатура;
  |Документ:=Storage.ОстаткиТМЦ.@LINK;
  |Вн:=Storage.ОстаткиТМЦ.Номенклатура.ВидНоменклатуры;Condition(Вн=пТовар);
  |зКН:=BegTotals(Количество);
  |зКП:=Income(Количество);
  |зКР:=Expense(Количество);
  |Group зСк,зН,Документ;";
  
  З:=Query.Create();
  З.Execute(ТЗ,2);
  
  Тб:=ТаблицаГоризонтальногоРазворота();
  While З.Next(1) Do
    пПр:=0;
    зТЦ:=?(З.зСк.рзТипЦенПоступления.Selected()=0,0,З.зСк.рзТипЦенПоступления);
    While З.Next(2) Do
      зН:=З.зН;
      Form.StatusText(""+З.зСк+":"+зН);
      If флЦентральнаяБаза=0 Then
        If зТЦ=0 Then
          If ТНом.FindAndGoto(зН,,"зН",1)=0 Then
            Continue;
          EndIf;
        ElseIf ТНом.FindAndGoto(зН.Code+"@"+Trim(зТЦ.Code),,"зН",1)=0 Then
          Continue;
        EndIf;
      Else
        If ТНом.FindAndGoto(зН.Code+"@"+Trim(З.зСк.рбПрефиксУдаленнойБазы),,"зН",1)=0 Then
          Continue;
        EndIf;
      EndIf;
      ТЦ:=ТНом.ТЦ;
      
      ОстК:=З.BegTotals("зКН");
      ОстТ:=ОстК;
      ДатаК:=НачДата;
      While З.Next(3) Do
        If isEmpty(З.Документ) Then
          Continue;
        EndIf;
        ДД:=BegOfDay(З.Документ.DocDate);
        While ДД>ДатаК Do
          пПр:=пПр+ОстК*Number(ТЦ.GetByName(Str(ДатаК)));
          ОстК:=ОстТ;
          ДатаК:=ДатаК+1;
        EndDo;
        ОстТ:=ОстТ+З.зКП-З.зКР;
      EndDo;
      While ДатаК<=КонДата Do
        пПр:=пПр+ОстК*Number(ТЦ.GetByName(Str(ДатаК)));
        ОстК:=ОстТ;
        ДатаК:=ДатаК+1;
      EndDo;
    EndDo;

    If пПр=0 Then
      Continue;
    EndIf;
    
    If Тб.FindAndGoto(З.зСк,,"Эл")=0 Then
      Тб.AddLine("Эл,ЭлР,Тб",З.зСк,З.зСк,ТаблицаГоризонтальногоРазворота(1));
    EndIf;
    Тб2:=Тб.Тб;
    
    Тб2.Set(1,"См",Тб2.Get(1,"См")+пПр);
  EndDo;
  Exit Тб;
EndFunction

Function ВычислениеДопКоэффПродажи(ТНом)
  If ТПродОбщ.FindAndGoto("0@0",,"Индекс")=0 Then
    ПродОтвет:=0;
    Exit 1;
  EndIf;
  ПродОтвет:=ТПродОбщ.Get(ТПродОбщ.CurLine,"ТНом"+ТНом);
  Exit 1;
EndFunction












Function Печать(СпУстановки)
  ТабФорма:=СпУстановки.GetByName("Установки");
  Т:=СпУстановки.GetByName("Таблица");
  НачДата:=BegOfDay(глПолучитьУстановку(ТабФорма,"НачДата"));
  КонДата:=BegOfDay(глПолучитьУстановку(ТабФорма,"КонДата"));
  оВО:=Form.оВО.Value.SelectedLine;
  оБезМПО:=Form.оБезМПО.Value;
  оНДС:=Form.оНДС.Value;
  оУровни:=Form.оУровни.Value;
  оПоказатели:=Form.оПоказатели.Value;


  ФлЛок:=0;
  If Т=0 Then
    If not глПроверитьДатуВОтчетах(Param,Form.НачДата.Value,Form.КонДата.Value,-1) Then
      Exit;
    EndIf;
    Т:=Table.Create();
    НачДата:=BegOfDay(Form.НачДата.Value);
    КонДата:=BegOfDay(Form.КонДата.Value);
  Else  
    ФлЛок:=1;
    Т.Lock();
    Т.Clear();
  EndIf;  
  
  Try
    ВсегоДней:=КонДата-НачДата+1;
    ФлСПоставщикамиС:=Number(DBVar.ИспользоватьПерепродажиВФинОтчетах);
    Внимание:=0;
    
    //1/(1+k/100)
    aEl:=Enum.СтавкиНДС;
    кфНДС:=List.Create();
    For i:=1 To aEl.Count() Do
      Стр:=Enum.GetByIndex("СтавкиНДС",i).ValueName;
      //НДС??
      If Pos("НДС",Стр)=1 Then
        TearStr(Стр,"НДС");
        Стр:=Number(Стр);
        кфНДС.Add(1/(1+Стр/100));
      Else
        кфНДС.Add(1);
      EndIf;
    EndDo;
  
    //Выводим шапку
    оЗагол:="Отчет по прибылям и убыткам"+?(оНДС=1," (суммы за минусом НДС)")+?(оВО=2," (отчет по начислению)");
    оСвойства:=?(НачДата=КонДата,"На "+FormatDate(НачДата,"dd.mm.YYYY (NN)"),"Период формирования с "+Str(НачДата)+" по "+Str(КонДата)+" (Дней: "+Str(КонДата-НачДата+1)+")");
    Сп:=СпУстановки;
    пМесяц:="Период с "+НачДата+" по "+КонДата;
    Т.CopyByX("v1|h1",1);Т.CopyByX("v1|h2");Т.CopyByX("v1|h3");
    Т.Options.FixedLine:=Т.Height();
    Баланс:=0;
    АктивНач:=Т.Height()+1;
    
    If not РасчетПолнойТаблицыПродаж() Then
      Exit;
    EndIf;
    
    ВывестиГруппировкуПростую("А","Маржинальная прибыль продаж",GetNothing(),ТбМарж,1);
    ВывестиГруппировкуПростую("А","Маржинальная прибыль поступлений",GetNothing(),ТбМарж2,1);
    
    //Оприходования/списания
    ТЗ:="зДата:=BegOfDay(Doc.РегистрацияТМЦ.DocDate);Condition((зДата>=НачДата)And(зДата<=КонДата));
    |зСтатус:=Doc.РегистрацияТМЦ.@Status;Condition(зСтатус>1);
    |зЦЗ:=Doc.РегистрацияТМЦ.ЦентрЗатрат;Condition(IsEmpty(зЦЗ));
    |зСкАкт:=Doc.РегистрацияТМЦ.Склад.ИспользоватьВАктиве;Condition(зСкАкт=1);
    |зСк:=Doc.РегистрацияТМЦ.Склад;
    |зДок:=Doc.РегистрацияТМЦ;
    |Group зДок;";
    З:=Query.Create();
    aTab:=З.Execute(ТЗ,0);
    
    Тб:=ТаблицаГоризонтальногоРазворота();
    Фл:=0;
    aTab.Select();
    While aTab.Next() Do
      Д:=aTab.зДок.Copy();
      Знак:=?(Д.КодОперации=Enum.коРегистрацияТМЦ.ОприходованиеТМЦ,1,-1);
      bTab:=Д.LineParts("Номенклатура");
      bTab.Select();
      While bTab.Next() Do
        Н:=bTab.Номенклатура;
        Фл:=1;
        
        ТГ:=Н.ТоварнаяГруппа;
        If Тб.FindAndGoto(ТГ,,"Эл")=0 Then
          Тб.AddLine("Эл,ЭлР,Тб",ТГ,ТГ,ТаблицаГоризонтальногоРазворота());
          Тб.Тб.NewLine();
        EndIf;
        См:=глПолучитьОсновнуюЦену(Н,Д.Склад,Д.DocDate)*bTab.Количество*Знак*?(оНДС=0,1,(1-глКоэффНДС(bTab.СтавкаНДС)));
        Тб2:=Тб.Тб;
        Тб2.CurLine:=1;
        Тб2.См:=Тб2.См+См;
      EndDo;
    EndDo;
    
    If Фл Then
      ВывестиГруппировкуПростую("А","Оприходование/списание ТМЦ",GetNothing(),Тб,1);
    EndIf;
    
    
    //Комплектация товара
    ТЗ:="зДата:=BegOfDay(Doc.КомплектацияТовара.DocDate);Condition((зДата>=НачДата)And(зДата<=КонДата));
    |зСтатус:=Doc.КомплектацияТовара.@Status;Condition(зСтатус>1);
    |зЦЗ:=Doc.КомплектацияТовара.ЦентрЗатрат;Condition(IsEmpty(зЦЗ));
    |зСкАкт:=Doc.КомплектацияТовара.Склад.ИспользоватьВАктиве;Condition(зСкАкт=1);
    |зСк:=Doc.КомплектацияТовара.Склад;
    |зДок:=Doc.КомплектацияТовара;
    |Group зДок;";
    З:=Query.Create();
    aTab:=З.Execute(ТЗ,0);
    
    Тб:=ТаблицаГоризонтальногоРазворота();
    Фл:=0;
    aTab.Select();
    While aTab.Next() Do
      Д:=aTab.зДок.Copy();
      bTab:=Д.LoadStorage("ОстаткиТМЦ");
      bTab.Select();
      While bTab.Next() Do
        Н:=bTab.Номенклатура;
        Фл:=1;
        
        ТГ:=Н.ТоварнаяГруппа;
        If Тб.FindAndGoto(ТГ,,"Эл")=0 Then
          Тб.AddLine("Эл,ЭлР,Тб",ТГ,ТГ,ТаблицаГоризонтальногоРазворота());
          Тб.Тб.NewLine();
        EndIf;
        См:=глПолучитьОсновнуюЦену(Н,Д.Склад,Д.DocDate)*bTab.Количество*?(aTab._Expense=0,1,-1)*?(оНДС=0,1,(1-глКоэффНДС(Н.СтавкаНДС)));
        Тб2:=Тб.Тб;
        Тб2.CurLine:=1;
        Тб2.См:=Тб2.См+См;
      EndDo;
    EndDo;
    
    If Фл Then
      ВывестиГруппировкуПростую("А","Комплектация товара",GetNothing(),Тб,1);
    EndIf;
    
    
    
    If оВО=2 Then
      ТбПлюс:=ПолучитьТаблицуОсобыхСкидокПоставщиков("Income");
    EndIf;
    
    //Скидки поставщиков
    ТЗ:="(зДата:=BegOfDay(Doc.РеализацияПрочее.DocDate);Condition((зДата>=НачДата)And(зДата<=КонДата));
    |зСтатус:=Doc.РеализацияПрочее.@Status;Condition(зСтатус>1);
    |зС0:=Doc.РеализацияПрочее.СуммаВзаиморасчетов;
    |зСм:=SUM(зС0);
    |зТипАкт:=Doc.РеализацияПрочее.Контрагент.ТипАктива;Condition(зТипАкт<>3);
    |зЭл:=Doc.РеализацияПрочее.Контрагент;
    |Group зЭл;) UNION ALL (
    |зДата:=BegOfDay(Doc.КорректировкаДолга.DocDate);Condition((зДата>=НачДата)And(зДата<=КонДата));
    |зСтатус:=Doc.КорректировкаДолга.@Status;Condition(зСтатус>1);
    |зС0:=Doc.КорректировкаДолга.СуммаВзаиморасчетов;
    |зСм:=SUM(зС0);
    |зСт:=Doc.КорректировкаДолга.СтатьяЗатрат;Condition(IsEmpty(зСт));
    |зТипАкт:=Doc.КорректировкаДолга.Контрагент.ТипАктива;Condition(зТипАкт<>3);
    |зЭл:=Doc.КорректировкаДолга.Контрагент;
    |Group зЭл;);";
    
    Тб:=ТаблицаГоризонтальногоРазворота();
    Form.StatusText("Запрос по скидкам поставщиков...");
    З:=Query.Create();
    З.Execute(ТЗ);
    While З.Next(1) Do
      См:=З.зСм;
      
      If оВО=2 Then
        If ТбПлюс.FindAndGoto(З.зЭл,,"Индекс") Then
          См:=См+ТбПлюс.См;
          ТбПлюс.Remove(ТбПлюс.CurLine);
        EndIf;
      EndIf;
      
      If not Тб.FindAndGoto(З.зЭл,,"Эл") Then
        Тб.AddLine("Эл,ЭлР,Тб",З.зЭл,З.зЭл,ТаблицаГоризонтальногоРазворота());
        Тб.Тб.NewLine();
      EndIf;
      Тб2:=Тб.Тб;
      Тб2.CurLine:=1;
      Тб2.См:=Тб2.См+См;
    EndDo;
    
    If оВО=2 Then
      ТбПлюс.Select();
      While ТбПлюс.Next() Do
        Тб.AddLine("Эл,ЭлР,Тб",ТбПлюс.Индекс,ТбПлюс.Индекс,ТаблицаГоризонтальногоРазворота());
        Тб2:=Тб.Тб;
        Тб2.NewLine();
        Тб2.См:=Тб2.См+ТбПлюс.См;
      EndDo;
    EndIf;
          
    ВывестиГруппировкуПростую("А","Скидки поставщиков",GetNothing(),Тб,1);
    
    
    
    
    If оБезМПО=0 Then
      Тб:=ПолучитьМаржПрибыльОстатков();
      If Тб=0 Then
        Exit;
      EndIf;
      ВывестиГруппировкуПростую("А","Маржинальная прибыль остатков",GetNothing(),Тб,1);
    EndIf;
    


    If АктивНач+1<Т.Height() Then
      Т.Area(2,АктивНач,2,Т.Height()).Merge();
    EndIf;
    БалансА:=Баланс;
    Баланс:=0;
    ПассивНач:=Т.Height()+1;
    
    //Затраты
    Form.StatusText("Запрос по затратам...");
    ТЗ:="Period From НачДата To КонДата;
    |зЦЗ:=Storage.Затраты.ЦентрЗатрат;
    |зВН:=Storage.Затраты.ЦентрЗатрат.ВнутреннееПеремещение;Condition(зВН=0);
    |зСЗ:=Storage.Затраты.СтатьяЗатрат;
    |зТЗ:=Storage.Затраты.ТипЗаписи;"+
    ?(оВО=1,"Condition(зТЗ<2);","Condition((зТЗ=0)Or(зТЗ=2));")+_NEWLINE+
    "зС0:=Storage.Затраты."+?(оНДС=0,"Сумма;","СуммаБезНДС;")+
    "зСм:=SUM(зС0);
    |Group зЦЗ,зСЗ;";
    
    Тб:=ТаблицаГоризонтальногоРазворота();
    З:=Query.Create();
    З.Execute(ТЗ);
    
    ТаблицаЗатрат:=Tab.Create("Индекс");
    ТаблицаЗатрат.AddColumn("См",,"Number");
    СпЦЗ:=List.Create();
    While З.Next(1) Do
      Тб2:=ТаблицаГоризонтальногоРазворота();
      Фл:=0;
      См0:=0;
      While З.Next(2) Do
        Фл:=1;
        
        Тб2.AddLine("Эл,ЭлР,Тб",З.зСЗ,З.зСЗ,ТаблицаГоризонтальногоРазворота());
        Тб3:=Тб2.Тб;
        Тб3.NewLine();
        Тб3.См:=-З.зСм;
        См0:=См0+З.зСм;

        Индекс:="0@@"+З.зСЗ.Code;
        ТаблицаЗатрат.AddLine("Индекс,См",Индекс,-З.зСм);
      EndDo;
      If not Фл Then
        Continue;
      EndIf;
      СпЦЗ.Add(З.зЦЗ);
      Тб.AddLine("Эл,ЭлР,Тб",З.зЦЗ,З.зЦЗ,Тб2);
      
      Индекс:="0@"+З.зЦЗ.Code;
      ТаблицаЗатрат.AddLine("Индекс,См",Индекс,-См0);
    EndDo;
    ВывестиГруппировкуПростую("П","Затраты и убытки",GetNothing(),Тб,2);
    If ПассивНач+1<Т.Height() Then
      Т.Area(2,ПассивНач,2,Т.Height()).Merge();
    EndIf;  
    
    
    Form.StatusText("Вычисление дополнительных показателей...");
    For аПок:=1 To оПоказатели.Size() Do
      If оПоказатели.Check(аПок)=0 Then
        Continue;
      EndIf;
    
      ФлВывод:=1;
      Тб:=ТаблицаГоризонтальногоРазворота();
      ТНом:=Number(оПоказатели.Get(аПок));
      СтрИмя:=оПоказатели.GetName(аПок);
    
      If ТНом<50 Then
        If not ВычислениеДопКоэффПродажи(ТНом) Then
          Exit;
        EndIf;
        Тб.AddLine("См",ПродОтвет);
      ElseIf ТНом=50 Then
        If not ВычислениеДопКоэффПродажи(1) Then
          Exit;
        EndIf;
        Тб.AddLine("См",?(Round(ПродОтвет,6)=0,0,(БалансА+Баланс)/ПродОтвет*100));
      ElseIf ТНом=51 Then
        If not ВычислениеДопКоэффПродажи(1) Then
          Exit;
        EndIf;
        Тб.AddLine("См",?(Round(ПродОтвет,6)=0,0,БалансА/ПродОтвет*100));
      ElseIf ТНом=52 Then
        For i2:=1 To СпЦЗ.Size() Do
          Тб:=ТаблицаГоризонтальногоРазворота();
          ЦЗ:=СпЦЗ[i2];
          См1:=0;
          If ТаблицаЗатрат.FindAndGoto("0@"+ЦЗ.Code,,"Индекс")>0 Then
            См1:=ТаблицаЗатрат.См;
          EndIf;
          If not ВычислениеДопКоэффПродажи(1) Then
            Exit;
          EndIf;
          Тб.AddLine("См",?(Round(ПродОтвет,6)=0,0,См1/ПродОтвет*100));
          ВывестиГруппировкуПростую("О","Отношение суммы """+ЦЗ+""" к сумме продаж (%)",GetNothing(),Тб,0);
        EndDo;
        ФлВывод:=0;
      Else
        ФлВывод:=0;
      EndIf;
      If ФлВывод Then
        ВывестиГруппировкуПростую("О",СтрИмя,GetNothing(),Тб,0);
      EndIf;
    EndDo;
    
    
    Form.StatusText("Вывод итогов...");
    //Итоги
    Т.CopyByX("v9|h1",1);
    пЗн:=Баланс+БалансА;
    пЗн:=глФРМ(пЗн);
    Т.CopyByX("v9|h2");
    Т.CopyByX("v9|h3");
    
    If оУровни=1 Then
      Т.Levels().Close();
      Т.Levels(1).Close();
    EndIf;
    глПечатнаяФормаОтчета(Т,Param,оЗагол);
  Finally
    If ФлЛок Then
      Т.Unlock();
      Т.EditorMode:=1;
    EndIf;
  EndTry;
EndFunction


Function OnExecute(СпУстановки)
  Печать(СпУстановки);
EndFunction

